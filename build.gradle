buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'

def skipProguard = project.hasProperty("skipProguard") && project.property("skipProguard") == "true";
if (skipProguard) {
    logger.warn('WARNING: Skipping proguard will only work for devices running Android 21+ !!')
}

def rev = project.hasProperty("versionCode") ? project.getProperty("versionCode") : 2000;

android {
    compileSdkVersion 'android-22'
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "org.animetwincities.animedetour"
        minSdkVersion  (skipProguard ? 21 : 16)
        targetSdkVersion 22
        versionCode rev
        versionName "2.2.0"
        multiDexEnabled skipProguard
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/license/THIRD-PARTY.txt'
    }
    signingConfigs {
        release {
            if (false == project.hasProperty("keystore_password")) {
                throw new GradleException("You must set a keystore password. See README");
            }

            storeFile file('animetwincities.keystore')
            storePassword project.getProperty("keystore_password")
            keyAlias "adprogramguide"
            keyPassword project.getProperty("keystore_password")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '+dev'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            minifyEnabled !skipProguard
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            minifyEnabled true
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile 'com.github.InkApplications:android-monolog:v0.1.0'
    compile 'com.github.InkApplications:Prism:v1.1.0@aar'
    compile 'com.inkapplications:android-simple-recycler-view:1.0.0'
    compile 'com.github.InkApplications:ground-control:v0.1.0'

    compile 'com.android.support:support-v4:22.2.0'
    compile "com.android.support:appcompat-v7:22.2.0"
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile "com.google.android.gms:play-services-base:7.5.0"
    compile "com.google.android.gms:play-services-maps:7.5.0"
    compile "com.google.android.gms:play-services-analytics:7.5.0"
    compile('com.google.maps.android:android-maps-utils:0.3.4') {
        exclude group: "com.google.android.gms"
    }
    compile 'com.getbase:floatingactionbutton:1.10.0'
    compile 'com.github.nirhart:parallaxscroll:1.0'

    compile 'com.github.frankiesardo:icepick:2.3.6'
    provided 'com.github.frankiesardo:icepick-processor:2.3.6'
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile('com.netflix.rxjava:rxjava-android:0.20.7') {
        exclude module: "rxjava-core"
    }
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.retrofit:converter-jackson:1.9.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile ('com.cedarsoft.serialization.commons-serializers:jackson:7.0.0') {
        exclude module: "xml-commons"
    }
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.5.0'
    compile 'joda-time:joda-time:2.8.1'
    compile 'org.joda:joda-convert:1.7'
    compile 'com.facebook.fresco:fresco:0.5.3+'
    compile "com.facebook.fresco:imagepipeline-okhttp:0.5.3+"

    compile 'com.facebook.stetho:stetho:1.1.1'
    compile 'com.facebook.stetho:stetho-okhttp:1.1.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}

apply from: 'analysis.gradle'
